#!/usr/bin/env python
import platform, os, sys

Import('env')
Import('env_modules')
env_module = env_modules.Clone()

def quickjs():
	version = open('quickjs/quickjs/VERSION', 'r').read().split('\n')[0]
	env_module.Append(CPPDEFINES={"QUICKJS_CONFIG_VERSION": '"'+ version +'"'})
	env_module.Append(CPPDEFINES=["CONFIG_BIGNUM"])
	if env_module['target'] == 'debug':
		env_module.Append(CPPDEFINES={"DUMP_LEAKS": 1})
	env_module.Append(CPPPATH=["quickjs/quickjs"])
	env_module.add_source_files(env.modules_sources, 'quickjs/*.cpp')
	env_module.add_source_files(env.modules_sources, 'quickjs/quickjs/*.c')

jsengine = 'quickjs'
if jsengine == 'quickjs':
	quickjs()
	
	
sources = [
	'register_types.cpp',
	'ecmascript_library.cpp',
	'ecmascript_language.cpp',
	'ecmascript_instance.cpp',
	'ecmascript.cpp',
]

if env['tools']:
	env_module.add_source_files(env.modules_sources, 'tools/*.cpp')

env_module.Append(CPPPATH=["#modules/ECMAScript"])
env_module.Append(CXXFLAGS=["-std=c++11"])
env_module.add_source_files(env.modules_sources, sources)
